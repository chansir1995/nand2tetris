// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static Array addr;
    /** Initializes the keyboard. */
    function void init() {
        let addr = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return addr[0];
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char cur, ret;
        let cur = 0;
        do Output.printChar(0); //black square
        while(cur = 0){
            let cur = addr[0];
        }
        let ret = cur;
        if((ret < 127) & (ret > 31)){  // 32~126 为可显示的字符
            do Output.backSpace();
            do Output.printChar(ret);
        }
        while(~(cur = 0)){
            let cur = addr[0];
        }
        return ret;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char cur, index;
        var String str;
        let str = String.new(80);
        do Output.printString(message);
        let index = 0;
        let cur = readChar();
        while(~(cur = 128)){
            if(cur = 129){          
                if(index > 0){
                    do str.eraseLastChar(); 
                    do Output.backSpace();
                    let index = index - 1;
                }
                do Output.backSpace();
                
                
            }else{
                do str.appendChar(cur); 
                let index = index + 1;       
            }
            let cur = readChar();
            
        }
        do Output.println();
        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String str;
        let str = readLine(message);
        return str.intValue();
    }
}
